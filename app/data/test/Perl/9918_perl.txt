package Adylay::Controller::Slack;

use Mojo::Base 'Mojolicious::Controller';
use Mojo::UserAgent;

sub notify {
  my $self = shift;

  my $model = $self->stash('model');
  my $selection = $self->flash('selection');

  my %response = $model->sendMessage("Switched profile to: $selection");
  if (!$response{success}) {
    $self->flash(error => $response{message});
  }
  $self->flash(selection => $selection);
  $self->redirect_to('/config');
}

sub message {
  my $self = shift;

  my $model = $self->stash('model');
  my $message = $self->req->json->{message};

  $self->app->log->info($message);
  my %response = $model->sendMessage($message);
  if ($response{success}) {
    $self->res->code(201);
    $self->render(json => { sent => $message });
  } else {
    $self->res->code(500);
    $self->render(json => { error => $response{message} });
  }
}

sub updateWebhook {
  my $self = shift;
  my $model = $self->stash('model');
  my $webhook = $self->req->body_params->param('webhook');
  $self->app->log->info("Updating slack webhook to: $webhook");
  my $successful = $model->updateWebhook($webhook);
  if ($successful) {
    $self->redirect_to('/config');
  } else {
    $self->flash(error => 'Failed to update config.');
    $self->redirect_to('/config');
  }
}

1;
