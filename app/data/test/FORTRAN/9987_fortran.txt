      SUBROUTINE LNCORN(IA,NPIX,NLINES,INVALA,ASCALE,AZERO,
     +                  BOTLIM,TOPLIM,TABLE,NTAB,
     +                  IB,INVALB,BSCALE,BZERO,
     +                  IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO APPLY A LINEARITY CORRECTION TO AN IMAGE FROM A LOOK-UP
*	TABLE
*
*METHOD
*	SCAN THE TABLE TO DETERMINE THE RANGE OF OUTPUT VALUES AND
*	CALCULATE OUTPUT SCALING TO PRESERVE ACCURACY IN THE OUTPUT.
*	APPLY THE CORRECTION BY INTERPOLATING LINEARLY BETWEEN THE
*	ENTRIES IN THE LOOK-UP TABLE
*
*ARGUMENTS
*	IA (IN)
*	INTEGER*2(NPIX,NLINES)
*		THE INPUT IMAGE
*	NPIX,NLINES (IN)
*	INTEGER
*		THE DIMENSIONS OF IA AND IB
*	INVALA (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IA
*	ASCALE,AZERO (IN)
*	REAL
*		SCALE AND ZERO LEVEL FOR IA
*	BOTLIM,TOPLIM (IN)
*	REAL
*		THE INPUT DATA VALUES CORRESPONDING TO THE FIRST AND
*		LAST LOOK-UP TABLE ENTRIES
*	TABLE (IN)
*	REAL(NTAB)
*		THE LOOK-UP TABLE
*	NTAB (IN)
*	INTEGER
*		THE NUMBER OF ENTRIES IN TABLE (2 OR MORE)
*	IB (OUT)
*	INTEGER*2(NPIX,NLINES)
*		THE OUTPUT IMAGE
*	INVALB (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IB
*	BSCALE,BZERO (OUT)
*	REAL
*		SCALE AND ZERO LEVEL FOR IB
*	IERR (OUT)
*	INTEGER
*		ERROR FLAG: ZERO FOR SUCCESS
*
*CALLS
*	NONE
*
*NOTES
*	USES INTEGER*2 ARRAYS
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      INTEGER*2 IA(NPIX,NLINES),IB(NPIX,NLINES)
      REAL TABLE(NTAB)
      PARAMETER (MININT=-32767,MAXINT=32767)
C
C CHECK ARGUMENT VALIDITY
C
      IERR=0
      IF(TOPLIM.LT.BOTLIM) THEN
	IERR=1
      ELSE IF(NTAB.LT.2) THEN
	IERR=2
      ELSE
C
C CALCULATE DATA INTERVAL BETWEEN TABLE ENTRIES
C
	DINTVL=MAX((TOPLIM-BOTLIM),1.0E-20)/(NTAB-1)
	RDINT=1.0/DINTVL
C
C SCAN THE TABLE TO FIND THE POSSIBLE EXTREMES OF OUTPUT VALUES
C
	OUTMAX=MININT
        OUTMIN=MAXINT
	DO 1 IDEN=1,NTAB
	  OUTMAX=MAX(OUTMAX,TABLE(IDEN))
	  OUTMIN=MIN(OUTMIN,TABLE(IDEN))
    1   CONTINUE
C
C SET SCALING OF OUTPUT VALUES SO AS TO USE 0.75 OF THE AVAILABLE
C OUTPUT INTEGER RANGE
C
	BSCALE=MAX(1.0E-20,OUTMAX-OUTMIN)/(0.75*(MAXINT-MININT))
	BZERO=((OUTMAX+OUTMIN)-(MAXINT+MININT))*0.5
	RBSCL=1.0/BSCALE
C
C SCAN IMAGE TO MAKE THE ITF CORRECTION
C
	DO 101 J=1,NLINES
	  DO 100 I=1,NPIX
C
C OUTPUT PIXEL IS INVALID IF INPUT PIXEL IS INVALID
C
	    IF(IA(I,J).EQ.INVALA) THEN
	      IB(I,J)=INVALB
	    ELSE
C
C OTHERWISE CALCULATE INPUT DATA VALUE AND CHECK IT IS WITHIN THE
C TABLE RANGE. IF NOT..OUTPUT IS INVALID
C
	      D=IA(I,J)*ASCALE+AZERO
	      IF(D.GT.TOPLIM.OR.D.LT.BOTLIM) THEN
		IB(I,J)=INVALB
	      ELSE
C
C IF OK, FIND ADJACENT TABLE ENTRIES AND INTERPOLATE LINEARLY BETWEEN
C THEM
C
		TABLOC=(D-BOTLIM)*RDINT+1.0
		NTAB1=INT(TABLOC)
		NTAB2=MIN(NTAB1+1,NTAB)
		DTAB=TABLOC-NTAB1
		DOUT=TABLE(NTAB1)*(1.0-DTAB)+TABLE(NTAB2)*DTAB
C
C ASSIGN RESULT TO OUTPUT PIXEL, APPROPRIATELY SCALED
C
		IB(I,J)=NINT((DOUT-BZERO)*RBSCL)
	      ENDIF
	    ENDIF
  100     CONTINUE
  101   CONTINUE
      ENDIF
      RETURN
      END
