      SUBROUTINE GTDSCR(FNAME,DNAME,TYPE,IVAL,RVAL,CVAL,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO OBTAIN DESCRIPTOR ITEMS FROM DATA FRAMES AND CONVERT TO
*	NUMERICAL VALUES IF REQUIRED
*
*METHOD
*	READ DESCRIPTOR ITEM AS A CHARACTER STRING, CONVERT TO REAL OR
*	INTEGER AS REQUIRED
*
*ARGUMENTS
*	FNAME (IN)
*	CHARACTER*(*)
*		NAME OF FRAME TO BE USED
*	DNAME (IN)
*	CHARACTER*(*)
*		NAME OF DESCRIPTOR ITEM
*	TYPE (IN)
*	CHARACTER*(*)
*		'REAL', 'INTEGER' OR 'CHARACTER': GIVES TYPE REQUIRED
*	IVAL (OUT)
*	INTEGER
*		RETURNS INTEGER VALUE IF REQUIRED
*	RVAL (OUT)
*	REAL
*		RETURNS REAL VALUE IF REQUIRED
*	CVAL (OUT)
*	CHARACTER*(*)
*		RETURNS CHARACTER VALUE
*	IERR (OUT)
*	INTEGER
*		ERROR FLAG: ZERO FOR SUCCESS
*
*STARLINK PARAMETERS
*	'FNAME'
*		THE PARAMETER NAME OF THE FRAME TO BE ACCESSED IS GIVEN
*		IN THE ARGUMENT 'FNAME'
*
*CALLS
*	STARLINK:
*		RDDSCR,CTOR,CTOI
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER DSCPT(1)*30,FNAME*(*),DNAME*(*),TYPE*(*),CVAL*(*)
C
C OBTAIN DESCRIPTOR ENTRY
C
      CALL RDDSCR(FNAME,DNAME,1,DSCPT,NVAL,IERR)
C
C IF OBTAINED OK, CONTINUE
C
      IF(IERR.EQ.0) THEN
	CVAL=DSCPT(1)
C
C IF TYPE IS 'REAL', CONVERT TO A REAL NUMBER
C
	IF(TYPE.EQ.'REAL') THEN
	  CALL CTOR(DSCPT(1),R,ISTAT)
	  IF(ISTAT.EQ.0) THEN
	    RVAL=R
	  ELSE
C
C RETURN ERROR CODE IF CONVERSION NOT OK
C
 	    IERR=ISTAT
	  ENDIF
C
C IF TYPE IS 'INTEGER', CONVERT TO AN INTEGER
C
	ELSE IF(TYPE.EQ.'INTEGER') THEN
	  CALL CTOI(DSCPT(1),I,ISTAT)
	  IF(ISTAT.EQ.0) THEN
	    IVAL=I
	  ELSE
C
C RETURN ERROR CODE IF CONVERSION NOT OK
C
	    IERR=ISTAT
	  ENDIF
C
C IF NOT 'REAL' OR 'INTEGER', RETURN WITH JUST A CHARACTER VALUE
C
	ENDIF
      ENDIF
      RETURN
      END
