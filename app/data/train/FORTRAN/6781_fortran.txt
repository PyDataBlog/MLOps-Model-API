      SUBROUTINE RADPRF(IA,NPIX,NLINES,INVAL,ILEVEL,SIG0,AXISR,THETA,
     +			RANGE,X,Y,NXY,WT,IDEV,FWHM,GAMMA,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO FIT A RADIAL PROFILE TO A SET OF STAR IMAGES
*
*METHOD
*	FOR EACH STAR, PUT THE DATA INTO BINS REPRESENTING ISOPHOTAL
*	ZONES , ALLOWING FOR IMAGE ELLIPTICITY. FORM A LINKED LIST
*	OF ALL THE PIXELS IN EACH ZONE, THEN PROCESS THE CONTENTS OF
*	EACH ZONE USING MODE TO REJECT ERRONEOUS DATA. FIT A GAUSSIAN
*	TO EACH BINNED STAR AND NORMALISE THE DATA TO UNIT CENTRAL
*	AMPLITUDE. PUT THE NORMALISED DATA INTO A SET OF BINS FILLED
*	WITH DATA FOR ALL STARS. CALL PRFFIT TO FIT A RADIAL PROFILE TO
*	THIS COMBINED DATA. PRINT THE RESULTS.
*	IF REQUIRED, PLOT THE MEAN PROFILE AND THE FITTED FUNCTION
*
*ARGUMENTS
*	IA (IN)
*	INTEGER*2(NPIX*NLINES)
*		THE INPUT IMAGE
*	NPIX,NLINES (IN)
*	INTEGER
*		DIMENSIONS OF IA
*	INVAL (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IA
*	ILEVEL (IN)
*	INTEGER
*		INTERACTION LEVEL: CONTROLS PRINTING OF RESULTS
*	SIG0 (IN)
*	REAL
*		STAR 'SIGMA' ACROSS THE MINOR AXIS. 
*	AXISR (IN)
*	REAL
*		THE STAR AXIS RATIO
*	THETA (IN)
*	REAL
*		THE INCLINATION OF THE STAR MAJOR AXIS TO THE X AXIS
*		IN RADIANS (X THROUGH Y POSITIVE)
*	RANGE (IN)
*	REAL
*		THE NUMBER OF STAR 'SIGMA'S OUT TO WHICH THE RADIAL
*		PROFILE IS FITTED
*	X,Y (IN)
*	REAL(NXY)
*		THE ACCURATE STAR CENTRE POSITIONS
*	NXY (IN)
*	INTEGER
*		THE NUMBER OF STARS
*	WT (IN)
*	REAL(NXY)
*		POSITIVE FOR STARS TO BE USED, ZERO FOR THOSE TO BE
*		OMITTED
*	IDEV (IN)
*	INTEGER
*		SELECTS 'NONE' OR THE OUTPUT GRAPHICS DEVICE
*			0: NONE
*			1: ARGS
*			2: TEKTRONIX
*			3: GOC
*			4: T4014
*			5: VERSATEC
*	FWHM (OUT)
*	REAL
*		AN OUTPUT ESTIMATE OF THE FULL WIDTH AT HALF MAXIMUM
*		ACROSS THE MINOR AXIS OF A STAR IMAGE
*	GAMMA (OUT)
*	REAL
*		STAR RADIAL PROFILE PARAMETER
*	IERR (OUT)
*	INTEGER
*		ERROR FLAG: ZERO FOR SUCCESS
*
*CALLS
*	THIS PACKAGE:
*		MODE,PRFFIT
*	STARLINK:
*		WRUSER
*	HIGR GRAPHICS:
*		HIGR_GZBGN,HIGR_DREBAR,HIGR_DRLINE,HIGR_GZEND
*
*NOTES
*	USES INTEGER*2 ARRAYS AND SUBROUTINE NAMES LONGER THAN 6
*	CHARACTERS
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
C
C SET MAXIMUM RADIUS TO BE CONSIDERED FOR THE RADIAL PROFILE
C
      PARAMETER (MAXRAD=50,
     +		 MAXPTS=(2*MAXRAD+1)**2,
C
C SET RESOLUTION (NO. OF BINS PER PIXEL SPACING) FOR BINNING THE RADIAL
C PROFILES OF EACH STAR (RESOL1) AND THE MEAN PROFILE (RESOL2)
C
     +		 RESOL1=2.5,
     +		 RESOL2=10.0,
C
C SET NUMBER OF RADIAL BINS REQUIRED
C
     +		 NBIN1=MAXRAD*RESOL1+1.0,
     +		 NBIN2=MAXRAD*RESOL2+1.0)
      PARAMETER (PBAD=0.01,
     +		 NITER=10,
     +		 NITFIT=20,
     +		 TOLFIT=0.0005)
C
C DIMENSION ARRAYS...NOTE IMAGE IS TREATED AS A 1 DIMENSIONAL ARRAY
C
      INTEGER*2 IA(NPIX*NLINES)
      REAL X(NXY),Y(NXY),WT(NXY),DATA(MAXPTS),PROFIL(0:NBIN2),
     +	   PROFWT(0:NBIN2),PROFR(0:NBIN2),R(0:NBIN1),DMODE(0:NBIN1)
      INTEGER LSTART(0:NBIN1),NXTADR(MAXPTS),IADR(MAXPTS),NPTS(0:NBIN1),
     +	      BIN,STAR,BIN2
      DOUBLE PRECISION SUMW,SUMF,SUMF2,SUMDF,SUMD
      CHARACTER TEXT*1,PRBUF*80
C
C CALCULATE CONSTANTS FOR RELATING RADIAL DISTANCE IN AN ELLIPTICAL
C STAR IMAGE TO EFFECTIVE DISTANCE ALONG THE MINOR AXIS
C
      IERR=0
      S=SIN(THETA)
      C=COS(THETA)
      RAXISR=(1.0/AXISR)**2
      CONSTA=S*S+C*C*RAXISR
      CONSTB=C*C+S*S*RAXISR
      CONSTC=2.0*C*S*(RAXISR-1.0)
C
C CALCULATE SCALE FACTORS FOR CONVERTING RADIAL DISTANCE TO BINS
C
      RSIG=1.0/SIG0
      RSCALE=RESOL1*2.0/MIN(2.0,SIG0)
      RSCL2=RESOL2*2.0/MIN(2.0,SIG0)
C
C FIND THE RADIUS LIMIT IN THE MINOR AXIS DIRECTION IMPOSED BY
C THE FITTING LIMIT 'RANGE'
C
      RLIMIT=RANGE*SIG0
C
C FIND THE SIZE OF SQUARE TO BE SCANNED AROUND EACH STAR TO 
C ACCOMMODATE THE RADIAL FITTING RANGE
C
      ISHIFT=MIN(MAX(1,NINT(RLIMIT*AXISR)),MAXRAD)
C
C FIND THE NUMBER OF BINS TO BE USED IN BINNING THE RADIAL PROFILES
C OF EACH STAR AND OF THE MEAN PROFILE
C
      MXBIN1=MIN(NBIN1,INT(RLIMIT*RSCALE))
      MXBIN2=MIN(NBIN2,INT(RLIMIT*RSCL2))
C
C INITIALLISE THE ARRAYS TO HOLD THE MEAN RADIAL PROFILE
C
      DO 41 BIN=0,NBIN2
	PROFR(BIN)=0.0
	PROFIL(BIN)=0.0
	PROFWT(BIN)=0.0
   41 CONTINUE
C
C CONSIDER EACH STAR WHICH HAS POSITIVE WEIGHT
C
      DO 991 STAR=1,NXY
	IF(WT(STAR).GT.1.0E-10) THEN
C
C FIND THE EDGES OF THE SEARCH SQUARE CENTRED ON THE STAR
C
	  I0=NINT(MIN(MAX(-1.0E8,X(STAR)),1.0E8))
	  J0=NINT(MIN(MAX(-1.0E8,Y(STAR)),1.0E8))
	  IMIN=MAX(1,I0-ISHIFT)
	  IMAX=MIN(NPIX,I0+ISHIFT)
	  JMIN=MAX(1,J0-ISHIFT)
	  JMAX=MIN(NLINES,J0+ISHIFT)
C
C INITIALLISE ARRAYS TO POINT TO THE START OF A LINKED LIST OF ALL
C THE PIXELS IN A GIVEN RADIAL BIN
C AND TO FORM MEAN RADII FOR EACH BIN
C
	  DO 37 BIN=0,MXBIN1
	    R(BIN)=0.0
	    NPTS(BIN)=0
	    LSTART(BIN)=0
   37	  CONTINUE
C
C SCAN THE SQUARE AROUND THE STAR, CALCULATING THE X AND Y
C DISPLACEMENTS FROM THE CENTRE
C
	  INDEX=0
	  DY=JMIN-Y(STAR)-1.0
	  DX0=IMIN-X(STAR)-1.0
	  DO 97 J=JMIN,JMAX
	    IMLOCN=(J-1)*NPIX+IMIN-1
	    DY=DY+1.0
	    DY2=DY*DY
	    DX=DX0
	    DO 96 I=IMIN,IMAX
	      IMLOCN=IMLOCN+1
	      DX=DX+1.0
C
C IF THE PIXEL IS VALID, CALCULATE THE EFFECTIVE RADIUS
C
	      IF(IA(IMLOCN).NE.INVAL) THEN
		DX2=DX*DX
		DXY=DX*DY
		RDASH=SQRT(CONSTA*DX2+CONSTB*DY2+CONSTC*DXY)
C
C FIND THE RADIAL BIN
C
		BIN=RDASH*RSCALE
		IF(BIN.LE.MXBIN1.AND.RDASH.LE.RLIMIT) THEN
C
C FORM SUMS FOR THE EFFECTIVE MEAN RADIUS
C
		  NPTS(BIN)=NPTS(BIN)+1
		  R(BIN)=R(BIN)+RDASH
C
C FORM A LINKED LIST OF ALL THE PIXELS IN THIS RADIAL BIN
C
		  INDEX=INDEX+1
		  IADR(INDEX)=IMLOCN
		  NXTADR(INDEX)=LSTART(BIN)
		  LSTART(BIN)=INDEX
		ENDIF
	      ENDIF
   96	    CONTINUE
   97	  CONTINUE
C
C INITIALLISE SUMS FOR FORMING A LEAST SQUARES GAUSSIAN FIT TO THE
C STAR RADIAL PROFILE AND BACKGROUND
C
	  SUMW=0.0D0
	  SUMF=0.0D0
	  SUMF2=0.0D0
	  SUMDF=0.0D0
	  SUMD=0.0D0
C
C CONSIDER EACH BIN WITH ONE OR MORE POINTS IN IT
C
	  DO 100 BIN=0,MXBIN1
	    IF(LSTART(BIN).GT.0) THEN
C
C EXTRACT THE PIXELS IN THIS BIN FROM THE LINKED LIST AND STORE
C IN ARRAY 'DATA'
C
	      ICOUNT=0
	      INDEX=LSTART(BIN)
  144	      IF(INDEX.GT.0) THEN
	        ICOUNT=ICOUNT+1
		DATA(ICOUNT)=IA(IADR(INDEX))
		INDEX=NXTADR(INDEX)
		GO TO 144
	      ENDIF
C
C CALL MODE TO FIND THE MOST LIKELY VALUE IN THE BIN
C
	      CALL MODE(DATA,ICOUNT,PBAD,NITER,0.1,DMODE(BIN),DSIG)
C
C FORM THE MEAN EFFECTIVE RADIUS FOR THE BIN
C
	      R(BIN)=R(BIN)/NPTS(BIN)
C
C FORM SUMS FOR FITTING A GAUSSIAN PROFILE
C USING THE NUMBER OF DATA POINTS AS A WEIGHT
C
	      FUNCT=EXP(-0.5*(R(BIN)*RSIG)**2)
	      FW=FUNCT*NPTS(BIN)
	      SUMW=SUMW+NPTS(BIN)
	      SUMF=SUMF+FW
	      SUMF2=SUMF2+(FUNCT*FW)
	      SUMDF=SUMDF+DMODE(BIN)*FW
	      SUMD=SUMD+DMODE(BIN)*NPTS(BIN)
	    ENDIF
  100	  CONTINUE
C
C SOLVE THE NORMAL EQUATIONS FOR THE GAUSSIAN FIT
C
	  DET=SUMF2*SUMW-SUMF*SUMF
	  IF(DET.NE.0.0) THEN
	    AMP=(SUMW*SUMDF-SUMD*SUMF)/DET
	    BACK=(SUMF2*SUMD-SUMDF*SUMF)/DET
C
C INSERT THE STAR PROFILE INTO THE MEAN PROFILE BINS
C
	    DO 141 BIN=0,MXBIN1
	      IF(LSTART(BIN).GT.0) THEN
		BIN2=R(BIN)*RSCL2
		IF(BIN2.LE.MXBIN2) THEN
C
C USE THE NO. OF POINTS AND THE STAR AMPLITUDE AS A WEIGHT
C
		  PROFIL(BIN2)=PROFIL(BIN2)+(DMODE(BIN)-BACK)*NPTS(BIN)
		  PROFR(BIN2)=PROFR(BIN2)+R(BIN)*AMP*NPTS(BIN)
		  PROFWT(BIN2)=PROFWT(BIN2)+AMP*NPTS(BIN)
		ENDIF
	      ENDIF
  141	    CONTINUE
	  ENDIF
	ENDIF
  991 CONTINUE
C
C CALCULATE THE MEAN RADIAL PROFILE AND ASSOCIATED RADII FROM THE
C BINNED DATA
C
      DO 167 BIN=0,MXBIN2
	IF(PROFWT(BIN).GT.0.0) THEN
	  PROFIL(BIN)=PROFIL(BIN)/PROFWT(BIN)
	  PROFR(BIN)=PROFR(BIN)/PROFWT(BIN)
	ENDIF
  167 CONTINUE
C
C SET INITIAL ESTIMATES OF PROFILE PARAMETERS AND CALL PRFFIT TO
C OBTAIN A FULL LEAST SQUARES FIT TO THE PROFILE
C
      
      AMP=1.0
      BACK=0.0
      GAMMA=2.0
      SIGMA=SIG0
      CALL PRFFIT(PROFIL,PROFWT,PROFR,MXBIN2+1,NITFIT,TOLFIT,
     +AMP,BACK,SIGMA,GAMMA,IERRF)
C
C IF FIT WAS NOT SUCCESSFUL, SET IERR=1
C
      IF(IERRF.NE.0) THEN
	IERR=1
C
C OTHERWISE PRINT RESULTS IF ILEVEL.GE.2
C
      ELSE
	IF(ILEVEL.GE.2) THEN
C
C CALCULATE FULL WIDTH HALF MAXIMUM SEEING
C
	  FWHM=2.0*SIGMA*(1.38629**(1.0/GAMMA))
	  WRITE(PRBUF,21)FWHM
   21	  FORMAT('   FWHM SEEING=',SS,G11.4)
	  CALL WRUSER(PRBUF,ISTAT)
	  CALL WRUSER(' ',ISTAT)
	  WRITE(PRBUF,22)GAMMA
   22	  FORMAT('   GAMMA=',SS,G11.4)
	  CALL WRUSER(PRBUF,ISTAT)
	  CALL WRUSER(' ',ISTAT)
	ENDIF
C
C PLOT RESULTS, IF GRAPHICS IS REQUIRED
C
	IF(IDEV.GT.0) THEN
C
C COMPRESS THE DATA ARRAYS TO REMOVE EMPTY BINS
C
	  NDATA=-1
	  DO 604 BIN=0,MXBIN2
	    IF(PROFWT(BIN).GT.0.0) THEN
	      NDATA=NDATA+1
	      PROFIL(NDATA)=PROFIL(BIN)-BACK
	      PROFR(NDATA)=PROFR(BIN)
	      PROFWT(NDATA)=0.0
	    ENDIF
  604	  CONTINUE
C
C WAIT FOR USER RESPONSE BEFORE PLOTTING
C
	IF (IDEV.NE.5) THEN
	      CALL WRUSER(' ',ISTAT)
	      CALL WRUSER('   ''RETURN'' TO PLOT FITTED RADIAL PROFILE',
     +	      ISTAT)
	      CALL RDUSER(TEXT,ISTAT)
	ENDIF
C
C PLOT AN ERROR BAR GRAPH OF THE DATA
C
	  CALL HIGR_GZBGN(4,0,IDEV,2)
	  CALL HIGR_DREBAR(PROFR,PROFIL,PROFWT,NDATA+1,
     *    'RADIAL DISTANCE#','INTENSITY#',
     *	  'MEAN STAR PROFILE#','#')
C
C CALCULATE THE FITTED PROFILE OVER THE DATA RANGE
C
	  RMAX=PROFR(NDATA)
	  DO 605 BIN=0,MXBIN2
	    RADIUS=(RMAX*BIN)/MXBIN2
	    PROFR(BIN)=RADIUS
	    PROFIL(BIN)=AMP*EXP(-0.5*((RADIUS/MAX(0.001,SIGMA))**GAMMA))
  605	  CONTINUE
C
C PLOT THE FITTED FUNCTION
C
	  CALL HIGR_DRLINE(PROFR,PROFIL,MXBIN2+1)
C
C WAIT FOR USER RESPONSE BEFORE CONTINUING
C
	  IF (IDEV.NE.5) CALL RDUSER(TEXT,ISTAT)
	  CALL HIGR_GZEND(4)
	ENDIF
      ENDIF
   99 RETURN
      END
