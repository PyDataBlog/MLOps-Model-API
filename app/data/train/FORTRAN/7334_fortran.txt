      module MOD_PRICOLR
      contains
C**********  MODULNAME: PRICOLR   ******* 21/05/87  12.36.48.******    78 KARTEN
      SUBROUTINE PRICOLR (NF,XLAMBDA,EMFLUX,RSTAR)
C***  CALCULATION AND PRINTOUT OF MAGNITUDES AND COLOR INDICES
C***  REFERENCES: SMITH,L.: 1968, MNRAS 140, P. 409
C***              HAYES,D.S. + LATHAM,D.W.: 1975, AP.J. 197, P. 593
C***              MASSEY,P.: 1984, AP.J. 281, P. 789
C***              WAMSTEKER,W.: 1981, A+A 97, P. 329
      use MOD_XRUDI
      use MOD_TCOLOR
      implicit real*8(a-h,o-z)
     
      DIMENSION XLAMBDA(NF),EMFLUX(NF)
      DIMENSION CENTER (13), ABAND(13), COLOR(13), CALCON(13), ADIFF(13)
	CHARACTER ITCOL*8
      DATA PI/3.1415926535898d0/
C***  1 PARSEC IN CM
      DATA PARSEC / 3.08561d18 /
C***  NAMES OF THE CONSIDERED COLOR BANDS
      DATA ABAND /3HUV1,3HUV2,
     $            7HSMITH U,7HSMITH B,7HSMITH V,8HMASSEY R,
     $            8HJ WE=.02,8HH WE=.02,8HK WE=.02,8HL WE=.02,8HM WE=.02
     $           ,8H(10) BSW,8HJOHNSONV/
      DATA ADIFF /1H ,7HUV1-UV2,1H ,3HU-B,3HB-V,3HV-R,7*1H /
C***  CENTER FREQUENCIES OF THE COLOR BANDS
      DATA CENTER /1445.,1940.,
     $             3650.,4270.,5160.,6000.,1.26E4,1.60E4,2.22E4,
     $             3.54E4,4.8E4,1.E5,5448./
C***  CALIBRATION CONSTANTS OF THE COLOR BANDS IN MAGNITUDES
      DATA CALCON / 6*48.64,
     $              49.488,49.921,50.440,51.605,51.924,53.574,
     $              48.593/
C***  SOURCES: U,B,V,R : HAYES + LATHAM
C***           V-Johns': Bessel & Brett 1988, PASP 100, 1134
C***                     central wavelength Bessel 1990
C***           JHKLM   : CAMPINS ET AL. 1985 ASTRON,J. 90,896
C***           (10)    : BARLOW,SMITH+WILLIS 1981 MNRAS 196,101
C***  DISTANCE MODULUS OF 10 PARSEC
      DISMO=-2.5d0*LOG10(RSTAR*RSTAR/100.d0/PARSEC/PARSEC )
     
      PRINT 1
    1 FORMAT (//,10X,'ABSOLUTE MONOCHROMATIC MAGNITUDES AND ',
     $      'COLOR INDICES',/,10X,51('='),//,
     $      10X,'BAND      WAVELENTH   MAGNITUDE',
     $ 8X,'MAG DIFFERENCES',3X,'COLOR TEMP',8X,'LOG F-L/F-L',/)
     
      DO 2 I=1,13
C***  INTERPOLATION OF THE EMERGENT FLUX AT CENTER FREQUENCY
      WAVENUM=1.E8/CENTER(I)
      IF (CENTER(I).GT.XLAMBDA(NF)) GOTO 2
      CALL XRUDI (FCENTER,WAVENUM,EMFLUX,XLAMBDA,1,NF,1)
C***  NOTE THAT THE ATROPHYSICAL FLUX MUST BE MULTIPLIED BY PI !!
      COLOR(I)=-2.5*LOG10(FCENTER*PI)+DISMO-CALCON(I)
      IF (CENTER(I) .LE. 10000.) THEN
         IF (ADIFF(I).EQ.1H ) THEN
         PRINT 3,ABAND(I),CENTER(I),COLOR(I)
    3    FORMAT (10X,A8,F10.0,' A ',F10.3)
         ELSE
         ITCOL='        '
         CALL TCOLOR (CENTER(I-1),CENTER(I),FCM1,FCENTER,ITCOL)
         COLDIF=COLOR(I-1)-COLOR(I)
         WAVFAC=LOG10(CENTER(I)/CENTER(I-1))*2.
         FLDIFF=COLDIF/(-2.5)+WAVFAC
         PRINT 6,ABAND(I),CENTER(I),COLOR(I),ADIFF(I),
     $           COLDIF,ITCOL,FLDIFF
    6    FORMAT(10X,A8,F10.0,' A ',F10.3,8X,A8,F7.3,5X,A8,8X,F7.3)
         ENDIF
         ELSE
         PRINT 4,ABAND(I),CENTER(I)/10000.,COLOR(I)
    4    FORMAT (10X,A8,F10.2,' MU',F10.3)
         ENDIF
      FCM1=FCENTER
    2 CONTINUE
     
C***  REDDENING-FREE COLOR INDICES (MASSEY, 1984)
C***  U-B STATT U-V WIE IN PAPER VON MASSEY         KORRIGIERT AM 27.11.85
C***  DELTA = (U-B) - 0.68* (B-V)
      DELTA=COLOR(3)-COLOR(4)-0.68*(COLOR(4)-COLOR(5))
C***  ETA = (B-V) - 1.46 * (V-R)
      ETA=COLOR(4)-COLOR(5)-1.46*(COLOR(5)-COLOR(6))
      PRINT 5,ETA,DELTA
    5 FORMAT (/,10X,'REDDENING-FREE INDICES (MASSEY, 1984):  ETA=',
     $      F7.3,10X,'DELTA=',F7.3)
     
      RETURN
      END subroutine
      end module
