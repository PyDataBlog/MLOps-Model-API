;-------------------------------------------------------------------------
;  Sprite Repositioning - "Gotta start with the basics"
;  By Johnny Starr (c) 2104
;-------------------------------------------------------------------------
            processor 6502
            include "./libs/macro.h"
            include "./libs/vcs.h"
            ORG     $F000           ; 4K - ROM begins here
;-------------------------------------------------------------------------
;  Equates and Variables
;-------------------------------------------------------------------------

;-------------------------------------------------------------------------
;  Setup
;-------------------------------------------------------------------------

START       SEI                     ; disable interrupts
            CLD                     ; clear decimal mode
            LDX     #$FF            ; 256
            TXS                     ; initialize stack
            LDA     #0              ; zero
CLEARMEM    STA     0,X             ; clear zero page, all RAM actually
            DEX                     ; decrease X
            BNE     CLEARMEM        ; loop
            

            ; setup background color
            LDA     #$00            ; setup background with first color
            STA     COLUBK           ; store
            
            ; setup player colors to contrast
            
            LDA     #$D2
            STA     COLUP0          ; player 0 color (red)
            STA     COLUP1
            
            LDA     #3
            STA     NUSIZ0
            STA     NUSIZ1
            
;-------------------------------------------------------------------------
;  Frame
;-------------------------------------------------------------------------
MAIN        LDA     #2                ; D1 = 1
            STA     WSYNC
            STA     VSYNC           ; enable VSYNC
            STA     VBLANK          ; disable video
            STA     WSYNC           ; send WSYNC signal for VSYNC
            STA     WSYNC           ; 2
            STA     WSYNC           ; 3
            LDA     #0              ; zero
            STA     VSYNC           ; turn off VSYNC

            LDA     #46             ; setup timer for vblank #46
            STA     TIM64T          ; 37 scan-lines * 76 / 64 = 44 (46)
             
            ;-------------------------------------------------------------
            ;  begin game logic
            ;-------------------------------------------------------------

   
            ;-------------------------------------------------------------
            ;  end game logic
            ;-------------------------------------------------------------
            
WAITVB      LDA     INTIM           ; check Vertical Blank timer
            BNE     WAITVB          ; repeat until zero
        
            LDA     #0              ; zero
            STA     VBLANK          ; enable video

            ;-------------------------------------------------------------
            ;  3 part Kernel
            ;-------------------------------------------------------------   
            
            LDX     #90
Top         
            



            
            DEX
            STA     WSYNC
            BNE     Top


Spawn      
            


            ; --- kernel 1 ---
            LDY     #12
Kernel:
            SLEEP   11
            
            LDA     SPRITE1,Y     
            STA     GRP0       
            STA     GRP1   

            SLEEP   2
            
            STA     RESP0   ; 3 (26)
            STA     RESP1   ; 3 (29)
            STA     RESP0   ; 3 (32)
            STA     RESP1   ; 3 (35)
            STA     RESP0   ; 3 (38)
            STA     RESP1   ; 3 (41)
            STA     RESP0   ; 3 (44)
            STA     RESP1   ; 3 (47)
            STA     RESP0   ; 3 (50)
            STA     RESP1   ; 3 (53)
            STA     RESP0   ; 3 (56)
            STA     RESP1   ; 3 (59)
            STA     RESP0   ; 3 (62)
            STA     RESP1   ; 3 (65)
            STA     RESP0   ; 3 (68)
            STA     RESP1   ; 3 (71)
            
            DEY             ; 2 (73)
            BNE     Kernel  ; 3 (76)
            ; --- end ---
            
            
            ; quickly turn off the player
            LDA     #0
            STA     GRP0
            STA     GRP1
            
            STA     WSYNC       ; kill 1 line
            STA     WSYNC       ; kill 2nd 
  
            
            LDX     #75
Bottom         
            DEX
            STA     WSYNC
            BNE     Bottom

            
            ;-------------------------------------------------------------
            ;  end drawing frame
            ;-------------------------------------------------------------
            
            LDA     #2              ; get ready to activate VBLANK
            STA     VBLANK          ; disable video
            
            LDA     #35             ; setup timer for overscan #35
            STA     TIM64T          ; 30 scan-lines * 76 / 64 = 35

            ;-------------------------------------------------------------
            ;  begin game logic
            ;-------------------------------------------------------------
            

            
            ;-------------------------------------------------------------
            ;  end game logic
            ;-------------------------------------------------------------
            
WAITOS      LDA     INTIM           ; check Overscan timer
            BNE     WAITOS          ; repeat until zero
            JMP     MAIN            ; begin next frame
                     
            
;-------------------------------------------------------------------------
;  Data Tables
;-------------------------------------------------------------------------
SPRITE1     EQU *-1
    .BYTE #%11100111
    .BYTE #%00100100
    .BYTE #%00100100
    .BYTE #%00100100
    .BYTE #%10111101
    .BYTE #%10111101
    .BYTE #%10111101
    .BYTE #%11111111
    .BYTE #%11111111
    .BYTE #%11111111
    .BYTE #%11100111
    .BYTE #%01111110
    .BYTE #%00111100
    
    
SPRITE2     EQU *-1
    .BYTE #%11100111
    .BYTE #%00100100
    .BYTE #%00100100
    .BYTE #%00100100
    .BYTE #%10111101
    .BYTE #%10111101
    .BYTE #%10111101
    .BYTE #%11111111
    .BYTE #%11111111
    .BYTE #%11111111
    .BYTE #%11100111
    .BYTE #%01111110
    .BYTE #%00111100    

    
;-------------------------------------------------------------------------
;  Vectors
;-------------------------------------------------------------------------
            ORG     $FFFC           ; End of ROM, 4K Total 
            .WORD   START           ; NMI
            .WORD   START           ; RESET
