//SWI dispatcher v1.1 (C)2006 by Rst7/CBSIE


                   EXTERN  __swihook_lib
	           EXTERN  __swihook_abort
                   
	           RSEG	   CODE:CODE
                   
	           CODE32
                   
                   PUBLIC   __swihandler_start
                   
                   
 __swihandler_start: 
 
                   STMFD   SP!, {R0-R3, LR}
                  
                   MRS     R0, SPSR
                   STMFD   SP!, {R0}
                   TST     R0, #0x20
                   LDRNEH  R0, [LR,#-2]
                   BICNE   R0, R0, #0xFF00
                   LDREQ   R0, [LR,#-4]
                   BICEQ   R0, R0, #0xFF000000

                   PUBLIC   __swihook_start
                   
 __swihook_start:
 
	           LDR	   R1, [SP, #0x00]      // Áåðåì CPSR
	           TST     R1, #0x20	        // Ïðîâåðÿåì íà THUMB (óñòàíîâêó T - áèòà)
	           ADREQ   R2, arm_jumper       // Áåðåì àäðåñ äæàìïåðà
	           BEQ	   arm_mode
                   
	           CMP	   R0, #199	        // Ñëóæåáíûé SWI äëÿ äëèííîé áèáëèîòåêè
	           LDREQH  R0, [LR], #2	        // Çàöåïëÿåì âòîðîå ñëîâî (äëèííûé íîìåð) c ïðîïóñêîì DW
                   STREQ   LR, [SP, #0x14]      // Ñîõðàíÿåì âðåìåííî àäðåñ âîçâðàòà (åñëè çàõî÷åì äàííûå)
	           ADD	   LR, LR, #0x01        // Óñòàíàâëèâàåì áèò 0, åñëè THUMB
	           ADR	   R2, thumb_jumper     // Áåðåì àäðåñ äæàìïåðà
                   
          arm_mode:
          
	           LDR	   R1, =__swihook_lib   // Óêàçàòåëü íà òàáëèöó àäðåñîâ
	           LDR	   R1, [R1]
	           CMP	   R1, #0x00
                   BEQ	   __swihook_exit
	           BIC	   R3, R0, #0x8000
                   CMP	   R3, #4096
                   BHI	   __swihook_exit
                   
	           TST	   R0,  #0x8000		// À íå àäðåñ íàì íàäî ïîëó÷èòü?
	           STMEQFD SP!, {LR}		// Êîïèðóåì àäðåñ âîçâðàòà èç LR_svc òîëüêî åñëè âûçîâ ôóíêöèè
	           LDMEQFD SP!, {LR}^		// â LR_usr, îí áóäåò èñïîëüçîâàí âûçûâàåìîé ôóíêöèåé
	           LDR     R12, [R1, R3, LSL#2]	// Áåðåì àäðåñ ôóíêöèè
	           STRNE   R12, [SP, #4]	// ïèøåì àäðåñ â R0(ñòåê)
    /* ADD */      MOV     R1,   LR
	           
                   BNE	  __swihook_exit
	           CMP	  R12, #0xFFFFFFFF
	           LDREQ  R2, =__swihook_abort
	           STREQ  R0, [SP, #4]
	           STR	  R2, [SP, #0x14]	// Ïèøåì àäðåñ äæàìïåðà äëÿ âîçâðàòà â ñòåêå (PC)
                   
                   
             
 __swihook_exit:
             
	           LDMFD  SP!, {R0}
	           MSR	  SPSR_cf, R0
	           LDMFD  SP!, {R0-R3, PC}^
      
        arm_jumper:
        
	           BX	  R12
                   
	           CODE16
                   
      thumb_jumper:
      
	           BX	  R12
                   
           nullsub:
           
	           BX	   LR
                   
	           END
