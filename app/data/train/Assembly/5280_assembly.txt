JSR get_devices
JSR activate_sped
JSR activate_screen
JSR show_title
JSR show_controls
JSR activate_keyboard
SET X, 90 ;rotation
JSR showX
JSR turn_sped
IAS interrupt

:loop ;end of program
SET PC, loop

:get_devices ;find hardware and store IDs
HWN I ;get number of devices
:get_devices_loop
SUB I, 1
HWQ I ;get info on device i
IFE B, 0x42ba
	JSR got_sped
IFE B, 0x30cf
	JSR got_keyboard
IFE B, 0x7349
	JSR got_screen
IFE I, 0
	SET PC, POP
SET PC, get_devices_loop

:got_sped
IFE A, 0xbf3c
	SET [spedID], I
SET PC, POP

:got_keyboard
IFE A, 0x7406
	SET [keyboardID], I
SET PC, POP

:got_screen
IFE A, 0xf615
	SET [screenID], I
SET PC, POP

:activate_sped
SET A, 1 ;memory map
SET X, model
SET Y, endModel
SUB Y, model
DIV Y, 2 ;number of verts
HWI [spedID]
SET PC, POP

:activate_screen
SET A, 0 ;turn on screen
SET B, 0x8000
HWI [screenID]
SET PC, POP

:show_title ;display text on screen
SET I, 0
:title_loop
SET J, [I + title]
IFE J, 0
	SET PC, POP
BOR J, 0x2f00
SET [I + 0x8000], J
ADD I, 1
SET PC, title_loop

:show_controls ;display text on screen
SET I, 0
:controls_loop
SET J, [I + controls]
IFE J, 0
	SET PC, POP
BOR J, 0xf000
SET [I + 0x8106], J
ADD I, 1
SET PC, controls_loop

:activate_keyboard ;turn on keyboard interrupts
SET A, 3
SET B, 33
HWI [keyboardID]
SET PC, POP

:interrupt ;on keyboard interrupt
SET A, 1
HWI [keyboardID]
IFG C, 70
	SUB C, 32
IFE C, 68
	ADD X, 15
IFE C, 65
	SUB X, 15
IFE X, 0xfff1 ;-15
	SET X, 345
IFG X, 359
	SET X, 0
JSR showX
JSR turn_sped
RFI A

:showX ;show value of X on screen
SET Y, X
MOD Y, 10
ADD Y, digits
SET Y, [Y]
BOR Y, 0xf000
SET [0x80d0], Y
SET Y, X
DIV Y, 10
MOD Y, 10
ADD Y, digits
SET Y, [Y]
BOR Y, 0xf000
SET [0x80cf], Y
SET Y, X
DIV Y, 100
MOD Y, 10
ADD Y, digits
SET Y, [Y]
BOR Y, 0xf000
SET [0x80ce], Y
SET PC, POP

:turn_sped
SET A, 2
HWI [spedID]
SET PC, POP

:spedID .dat 0 ;IDs of hardware
:keyboardID .dat 0
:screenID .dat 0

:digits
.ascii "0123456789"
:title
.asciiz "3D Creeper Program by Sir_Tiffy!"
:controls
.asciiz "Use A and D to turn"

:model ;Coded manually
	   ;93 verts total

;leg 1 20 verts
.dat 0x6060,0x248
.dat 0x6060,0x218
.dat 0x6070,0x218
.dat 0x6060,0x218

.dat 0x8060,0x218
.dat 0x8070,0x218
.dat 0x8070,0x248
.dat 0x8060,0x248
.dat 0x8060,0x218

.dat 0xa060,0x218
.dat 0xa070,0x218
.dat 0xa060,0x218
.dat 0xa060,0x248
.dat 0xa070,0x248
.dat 0xa070,0x218
.dat 0x6070,0x218
.dat 0x6070,0x248
.dat 0x6060,0x248
.dat 0xa060,0x248
.dat 0xa070,0x248
;leg 2 20 verts
.dat 0xa0a0,0x248
.dat 0xa0a0,0x218
.dat 0xa090,0x218
.dat 0xa0a0,0x218
.dat 0x60a0,0x218
.dat 0x60a0,0x248

.dat 0x80a0,0x248
.dat 0x80a0,0x218
.dat 0x8090,0x218
.dat 0x8090,0x248
.dat 0x80a0,0x248

.dat 0xa0a0,0x248
.dat 0xa090,0x248
.dat 0xa090,0x218
.dat 0x6090,0x218
.dat 0x60a0,0x218
.dat 0x60a0,0x248
.dat 0x6090,0x248
.dat 0x6090,0x218
.dat 0x6090,0x248

;body 16 verts
.dat 0x6070,0x248
.dat 0xa070,0x248
.dat 0xa090,0x248
.dat 0x6090,0x248
.dat 0x6070,0x248
.dat 0x6070,0x2a8
.dat 0xa070,0x2a8
.dat 0xa070,0x248
.dat 0xa070,0x2a8
.dat 0xa090,0x2a8
.dat 0xa090,0x248
.dat 0xa090,0x2a8
.dat 0x6090,0x2a8
.dat 0x6090,0x248
.dat 0x6090,0x2a8
.dat 0x6070,0x2a8
;head 16 verts
.dat 0x6060,0x2a8
.dat 0x6060,0x2e8
.dat 0xa060,0x2e8
.dat 0xa060,0x2a8
.dat 0x6060,0x2a8
.dat 0x60a0,0x2a8
.dat 0x60a0,0x2e8
.dat 0x6060,0x2e8
.dat 0x60a0,0x2e8
.dat 0xa0a0,0x2e8
.dat 0xa060,0x2e8
.dat 0xa0a0,0x2e8
.dat 0xa0a0,0x2a8
.dat 0xa060,0x2a8
.dat 0xa0a0,0x2a8
.dat 0x60a0,0x2a8
;face 21 verts
.dat 0x70a0,0x5a8
.dat 0x78a0,0x5a8
.dat 0x78a0,0x5b0
.dat 0x88a0,0x5b0
.dat 0x88a0,0x5a8
.dat 0x90a0,0x5a8
.dat 0x90a0,0x5c0
.dat 0x88a0,0x5c0
.dat 0x88a0,0x5c8
.dat 0x98a0,0x5c8
.dat 0x98a0,0x5d8
.dat 0x88a0,0x5d8
.dat 0x88a0,0x5c8
.dat 0x78a0,0x5c8
.dat 0x78a0,0x5d8
.dat 0x68a0,0x5d8
.dat 0x68a0,0x5c8
.dat 0x78a0,0x5c8
.dat 0x78a0,0x5c0
.dat 0x70a0,0x5c0
.dat 0x70a0,0x5a8
:endModel
