Imports System.Runtime.InteropServices
Imports System.Collections.ObjectModel
Imports System.ComponentModel

Public Class BaseUserControl

    ' Holds the parent form at the beginning of the creation.
    Private mParentForm As Form

    ' User controls don't have a shown event. Defining one.
    Public Event Shown(sender As Object)

    Public Shadows ReadOnly Property ParentForm As Form
        Get
            Return mParentForm
        End Get
    End Property

    Public Shadows Property AutoScroll As Boolean
        Get
            Return MyBase.AutoScroll
        End Get
        Set(value As Boolean)
            If (value) Then
                MyBase.AutoScroll = True
            Else
                ' Disable autoscroll preserving the scroll position.
                Dim scrollPosition As Point = Me.AutoScrollPosition
                MyBase.AutoScroll = False

                Me.HorizontalScroll.Value = scrollPosition.X * -1
                Me.VerticalScroll.Value = scrollPosition.Y * -1
            End If
        End Set
    End Property

    Protected Function IsMouseWithinControl(vControl As Control) As Boolean
        ' Get mouse point in control coordinates.
        Dim point As System.Drawing.Point = vControl.Parent.PointToClient(Control.MousePosition)

        ' Is the mouse out of the boundaries of the control?
        Return vControl.Bounds.Contains(point) AndAlso Form.ActiveForm Is ParentForm
    End Function

    Private Sub Control_HandleCreated() Handles Me.HandleCreated
        ' TODO getting the parent form at the beginning doesn't guarantee correctness if the control changes to a different form.
        mParentForm = Me.FindForm()

        ' After the handle is created, the component is being displayed.
        RaiseEvent Shown(Me)
    End Sub
End Class
