Namespace Data

    Public Class WatcherTimer
        Implements IWatcher

        Public Event Changed() Implements IWatcher.Changed

        Private WithEvents tmrRefresh As System.Timers.Timer = Nothing

        Public Sub New(dInterval As Double)
            tmrRefresh = New System.Timers.Timer
            tmrRefresh.Stop()
            tmrRefresh.Interval = dInterval
        End Sub

        Public Sub Start() Implements IWatcher.Start
            OnChanged()
        End Sub

        Public Sub [Stop]() Implements IWatcher.Stop
            tmrRefresh.Stop()
        End Sub

        Private Sub tmrRefresh_Elapsed(sender As Object, e As System.Timers.ElapsedEventArgs) Handles tmrRefresh.Elapsed
            OnChanged()
        End Sub

        Private Sub OnChanged()
            tmrRefresh.Stop()
            RaiseEvent Changed()
            tmrRefresh.Start()
        End Sub

#Region " IDisposable "

        Private _Disposed As Boolean = False

        Protected Overridable Sub Dispose(disposing As Boolean)
            If Not Me._Disposed Then
                If disposing Then
                    Me.Stop()
                End If
            End If
            Me._Disposed = True
        End Sub

        Public Sub Dispose() Implements IWatcher.Dispose
            Dispose(True)
            GC.SuppressFinalize(Me)
        End Sub

#End Region

    End Class

End Namespace