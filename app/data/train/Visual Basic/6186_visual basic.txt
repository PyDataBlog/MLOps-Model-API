Public Class GameBannerMedium

    ' Redefining the mouse click event in order to be able to raise it.
    Public Shadows Event MouseClick(sender As Object, e As MouseEventArgs)

    ' Redeclare the paint event to trigger it when the picturebox paints.
    Public Shadows Event Paint(sender As Object, e As PaintEventArgs)

    Public Property GameImage As Image
        Get
            Return PictureGameImage.Image
        End Get
        Set(value As Image)
            ' Replace image but dispose the current one first (if any)
            If (PictureGameImage.Image IsNot Nothing) Then
                Dim tmp As Image = PictureGameImage.Image
                PictureGameImage.Image = value
                tmp.Dispose()
            Else
                PictureGameImage.Image = value
            End If
        End Set
    End Property

    Public Property DropShadow As Boolean
        Get
            Return ShadowTop.Visible
        End Get
        Set(value As Boolean)
            ' If there is no change, return.
            If (value = DropShadow) Then
                Return
            End If

            Me.SuspendLayout()

            ' Change the visibility of the shadow accordingly.
            ShadowTop.Visible = value
            ShadowTopRight.Visible = value
            ShadowRight.Visible = value
            ShadowBottomRight.Visible = value
            ShadowBottom.Visible = value
            ShadowBottomLeft.Visible = value
            ShadowLeft.Visible = value
            ShadowTopLeft.Visible = value

            ' Add or substract the size that the table borders take.
            If (value) Then
                Me.Size += New Size(12, 12)
                Me.Location -= New Point(6, 6)
                Me.Margin -= New Padding(6, 6, 6, 6)
            Else
                Me.Size -= New Size(12, 12)
                Me.Location += New Point(6, 6)
                Me.Margin += New Padding(6, 6, 6, 6)
            End If

            Me.ResumeLayout()
        End Set
    End Property

    Public ReadOnly Property DefaultPictureSize As Size
        Get
            Return PanelImagesContainer.MinimumSize
        End Get
    End Property

    Public ReadOnly Property PictureSize As Size
        Get
            Return PanelImagesContainer.Size
        End Get
    End Property

    Private Sub Control_MouseClick(sender As Object, e As System.Windows.Forms.MouseEventArgs) Handles PictureGameImage.MouseClick, PictureOverlayHighlight.MouseClick
        ' Clicking anywhere but the shadow will raise the mouse click event.
        RaiseEvent MouseClick(Me, e)
    End Sub

    Private Sub Control_MouseEnter() Handles PictureGameImage.MouseEnter, PictureOverlayHighlight.MouseEnter
        ' When the mouse enters any part of the control (excluding shadow), highlight everything (excluding shadow).
        PictureOverlayHighlight.Visible = True
    End Sub

    Private Sub Control_MouseLeave() Handles PictureOverlayHighlight.MouseLeave
        ' Is the mouse out of the boundaries?
        If (Not IsMouseWithinControl(PanelImagesContainer)) Then
            ' Change the button image back to normal.
            PictureOverlayHighlight.Visible = False
        End If
    End Sub

    Private Sub PictureGameImage_Paint(sender As Object, e As PaintEventArgs) Handles PictureGameImage.Paint
        RaiseEvent Paint(Me, e)
    End Sub
End Class
