Imports System.Data
Imports System.Data.SqlClient

Namespace Data

    Public Class ConnectionManagerMSSQL
        Implements IConnectionManager

        Protected _Connection As SqlConnection
        Protected _ConnectionString As String

        Public Sub New(connectionString As String)
            _ConnectionString = connectionString
            InitConnection()
        End Sub

        Private Sub Connection_InfoMessage(sender As Object, e As SqlInfoMessageEventArgs)
            RaiseEvent InfoMessage(Me, New Models.InfoMessageEventArgs(e.Message))
        End Sub

#Region " IConnectionManager "

        Public Event InfoMessage(sender As Object, e As Models.InfoMessageEventArgs) Implements IConnectionManager.InfoMessage

        Public ReadOnly Property ConnectionString As String Implements IConnectionManager.ConnectionString
            Get
                Return _ConnectionString
            End Get
        End Property

        Public ReadOnly Property DBConnection As IDbConnection Implements IConnectionManager.DBConnection
            Get
                InitConnection()
                Return _Connection
            End Get
        End Property

        Public ReadOnly Property OpenDBConnection As IDbConnection Implements IConnectionManager.OpenDBConnection
            Get
                InitConnection()
                If Not (_Connection.State = ConnectionState.Open) Then
                    _Connection.Open()
                End If
                Return _Connection
            End Get
        End Property

        Public Sub CloseConnection() Implements IConnectionManager.CloseConnection
            If _Connection IsNot Nothing AndAlso Not (_Connection.State = ConnectionState.Closed) Then
                _Connection.Close()
                _Connection.Dispose()
            End If
            _Connection = Nothing
        End Sub

        Public Sub InitConnection() Implements IConnectionManager.InitConnection
            If _ConnectionString.IsNotSet() Then
                CloseConnection()
                Return
            End If

            If _Connection Is Nothing Then
                _Connection = New SqlConnection()
                AddHandler _Connection.InfoMessage, AddressOf Connection_InfoMessage
                _Connection.ConnectionString = _ConnectionString
            Else
                If _Connection.ConnectionString.IsNotSet() OrElse _Connection.ConnectionString.IsNotEqualTo(_ConnectionString) Then
                    CloseConnection()
                    InitConnection()
                End If
            End If
        End Sub

#End Region

#Region " IDisposable "

        Private _IsDisposed As Boolean = False

        Protected Overridable Sub Dispose(disposing As Boolean)
            If Not _IsDisposed Then
                If disposing Then
                    Me.CloseConnection()
                End If
            End If
            _IsDisposed = True
        End Sub

        Public Sub Dispose() Implements IDisposable.Dispose
            Dispose(True)
            GC.SuppressFinalize(Me)
        End Sub

#End Region

    End Class

End Namespace