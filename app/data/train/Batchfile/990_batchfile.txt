@echo off
echo.
REM Initializing main variables like paths of modules, current language, etc.
set PATH=%PATH%;%~dp0;%~dp0usr\baseutils;%~dp0usr\ketroutils
set ketro.ongoing.origin=%~dp0
call usr\ketroutils\konfparse ketro ketro
REM Is there specified any flag?
set ketro.ongoing.1=%1
if not defined ketro.ongoing.currentlang (call usr\ketroutils\langset eng)
if not defined ketro.ongoing.1 (goto invalidcall)
REM If specified, then calling right module
if %ketro.ongoing.1%==--help (goto showhelp)
if %ketro.ongoing.1%==-h (goto showhelp)
if %ketro.ongoing.1%==-shell (goto loadshell)
REM If no one flag specified or it's the wrong flag, showing an error message and exiting with errorcode 1.
:invalidcall
echo KLoad was launched with invalid parameters or without them. Use -h or --help flags to get help.
exit /b 1
REM If there is a command to start Ketro shell
:loadshell
REM Cosmetic preparations
call mode con cols=132 lines=64
call usr\ketroutils\titleset KLoad
REM Checking modules availability
if not exist %ketro.ongoing.origin%usr\ketroutils\konfman.cmd (goto noneededfiles)
if not exist %ketro.ongoing.origin%usr\ketroutils\konfparse.cmd (goto noneededfiles)
if not exist %ketro.ongoing.origin%shell.cmd (goto noneededfiles)
if not exist %ketro.ongoing.origin%usr\ketroutils\baseutil.cmd (goto noneededfiles)
REM Looks like everything is fine. Base initialization done, checking user's configuration file availability
set /a ketro.ongoing.isinitialized=1
if not exist data\user\profile.konf (goto launchkonfmannocfg)
REM Finally, starting shell
call shell
REM When shell session is done, exiting
echo Bye! Closed with errorcode %ERRORLEVEL%
exit /b
:showhelp
REM Showing manpage for kload
call usr\ketroutils\man kload
exit /b 0
:noneededfiles
REM If some of framework's files are not available, showing an error message and exiting
echo Failed to find some of Ketro Framework's files. Please, reinstall Ketro.
exit /b 2
:launchkonfmannocfg
REM Launching 1st Time Init module. It will configure user's profile settings
call usr\ketroutils\ftinit
REM Starting shell
call shell
REM When shell session is done, exiting
echo Bye! Closed with errorcode %ERRORLEVEL%
exit /b 0