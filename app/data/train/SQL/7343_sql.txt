
-- =============================================
-- Author:		zmgowin
-- Create date: 2013-05-21
-- Description:	提现-线下提现-会员线下提现
-- =============================================
ALTER PROCEDURE [dbo].[pB_Withdraw_User_Offline]
	@User_Id  numeric(10),				 --会员ID
	@Withdraw_Amount  numeric(16,6),     --提现金额：元
	@Busi_No numeric(10),                --业务流水号
	@Retval  int output,                 --返回标志。0为成功，其他为异常
	@Retinf  nvarchar(1024) output       --返回信息。0为成功，其他为Err_Msg 
AS
BEGIN
	set nocount on;
	declare @L_Busi_No nvarchar(16);			--业务流水号：流水号由2位的分类前缀+4位的一级分类序号+原10位流水号组合构成
	
	begin try
		if(@User_Id is null or @Withdraw_Amount is null or @Busi_No is null)
			begin
				set @Retval=600301;
				set @Retinf='过程或函数需要参数,但未提供该参数。';
				return;				
			end  
	
		--传入参数异常判断   
		if(@Withdraw_Amount<0)
			begin
				set @Retval=600303;
				set @Retinf='传入金额参数无效。';
				return;
			end 
		
		set @L_Busi_No='WI0501' + cast(@Busi_No as varchar);
		
		if(dbo.fn_Chk_Account_Enough(1,@User_Id,1010,0,@Withdraw_Amount)=0)
			begin
				--提现-线下提现-会员线下提现
				exec pS_Withdraw_User_Offline @User_Id,@Withdraw_Amount,@L_Busi_No,@Retval output,@Retinf output;
				if(@Retval>0) return;	
			end
		else
			begin
				set @Retval = 600201 ; 
				set @Retinf = '会员网站现金账户余额不足。' ;
				return;				
			end
    end try
    begin catch
		select @Retval=ERROR_NUMBER(),@Retinf=ERROR_MESSAGE();
    end catch
END
