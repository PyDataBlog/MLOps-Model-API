Imports Visomes.MySQL

Public Class ListProdutosOS
    Inherits ViewImportacao

    Private idOs As String
    Private idItemOrc As Integer
    Private indexItemOrc As Integer
    Private countItemFat As Integer
    Private indexItem As Integer
    Private frmFaturamento As CadFaturamento
    Private block As Boolean

    ''' <summary>
    ''' INICIA UMA NOVA INSTÂNCIA DE frmViewItemsRecList
    ''' </summary>
    ''' <param name="idOs"></param>
    ''' <remarks></remarks>
    Public Sub New(ByVal idOs As String, _
                   ByVal idItemOrc As Integer, _
                   ByVal indexItemOrc As Integer, _
                   ByRef frm As CadFaturamento)
        MyBase.New("Módulo Logística - Lista de Produtos da Os", TypeInit.ImportFaturamento)
        Me.idOs = idOs
        Me.idItemOrc = idItemOrc
        Me.indexItemOrc = indexItemOrc
        Me.frmFaturamento = frm
        Me.Type = TypeInit.ImportFaturamento
        CarregaLista()
    End Sub

    ''' <summary>
    ''' INICIA UMA NOVA INSTÂNCIA DE frmViewItemsRecList
    ''' </summary>
    ''' <param name="idOs"></param>
    ''' <remarks></remarks>
    Public Sub New(ByVal idOs As String)
        MyBase.New("Módulo Logística - Lista de Produtos da Os", TypeInit.ImportFaturamento)
        Me.idOs = idOs
        Me.Type = TypeInit.VIEW
        CarregaLista()
    End Sub

    ''' <summary>
    ''' VALOR TOTAL FATURADO
    ''' </summary>
    ''' <value></value>
    ''' <returns></returns>
    ''' <remarks></remarks>
    Public ReadOnly Property TotalFaturado() As Integer
        Get
            Return Me.countItemFat
        End Get
    End Property

    ''' <summary>
    ''' SETA INDEX DOS INDEX
    ''' </summary>
    ''' <remarks></remarks>
    Private Sub SetIndexItens()
        Dim index As Integer = 0
        For Each row As DataGridViewRow In MyBase.DataGridView.Rows
            index += 1
            row.Cells(3).Value = index
        Next
        Me.indexItem = index
    End Sub

    ''' <summary>
    ''' CARREGA PRODUTOS
    ''' </summary>
    ''' <remarks></remarks>
    Public Overrides Sub CarregaLista()
        Me.block = True
        Select Case Me.Type
            Case TypeInit.VIEW
                MyBase.DataGridView.DataSource = New OrdemServico(Me.idOs).CarregaProdutosView()
                For Each c As DataGridViewColumn In MyBase.DataGridView.Columns
                    c.ReadOnly = True
                Next
                IndexGrid()
                MyBase.DataGridView.AlternatingRowsDefaultCellStyle.BackColor = Color.Beige
            Case TypeInit.ImportFaturamento
                MyBase.DataGridView.DataSource = New OrdemServico(Me.idOs).CarregaProdutosFaturamento(idItemOrc, indexItem)
                For Each c As DataGridViewColumn In MyBase.DataGridView.Columns
                    c.ReadOnly = True
                Next
                For Each row As DataGridViewRow In MyBase.DataGridView.Rows
                    row.Cells(1).Value = Me.indexItemOrc
                Next
                ColorirGrid()
        End Select
        With MyBase.DataGridView()
            .Columns(0).Visible = False
            .Columns(1).HeaderText = ""
            .Columns(2).HeaderText = "Produto"
            .Columns(3).HeaderText = "Situação da OS"
            .Columns(4).HeaderText = "Situação do item da OS"
            .Columns(5).HeaderText = "Faturado"
            .Columns(6).Visible = False
        End With
        Me.block = False
    End Sub

    ''' <summary>
    ''' COLORI GRID
    ''' </summary>
    ''' <remarks></remarks>
    Private Sub ColorirGrid()
        If Me.Type = TypeInit.ImportFaturamento Then
            For Each row As DataGridViewRow In MyBase.DataGridView.Rows
                If CBool(row.Cells(5).Value) Then
                    row.DefaultCellStyle.ForeColor = Color.Blue
                End If
                If Not CBool(row.Cells(6).Value) Then
                    row.DefaultCellStyle.ForeColor = Color.Red
                End If
            Next
        End If
    End Sub

    ''' <summary>
    ''' IMPORTA ITENS
    ''' </summary>
    ''' <remarks></remarks>
    Public Overrides Sub btnImportar_Click(ByVal sender As System.Object, ByVal e As System.EventArgs)
        Importar()
        Me.DialogResult = Windows.Forms.DialogResult.OK
        Me.Close()
    End Sub

    ''' <summary>
    ''' EXPORTA ITENS
    ''' </summary>
    ''' <remarks></remarks>
    Private Sub Importar()
        Select Case Me.Type
            Case TypeInit.ImportFaturamento
                Dim selectedRowCount As Integer = _
                   MyBase.DataGridView.Rows.GetRowCount(DataGridViewElementStates.Selected)
                If selectedRowCount > 0 Then
                    MyBase.ProgressBar.Maximum = MyBase.DataGridView.RowCount
                    MyBase.ProgressBar.Visible = True
                    Application.DoEvents()
                    Dim i As Integer
                    For i = 0 To selectedRowCount - 1
                        If Not CBool(MyBase.DataGridView.SelectedRows(i).Cells(6).Value) Then
                            MessageBox.Show("Esse item está inativado!", _
                                            Core.STR_NOME_SISTEMA)
                        ElseIf CBool(MyBase.DataGridView.SelectedRows(i).Cells(5).Value) Then
                            MessageBox.Show("Esse item já foi faturado!", _
                                            Core.STR_NOME_SISTEMA)
                        Else
                            Me.frmFaturamento.AddProdutosOs(CInt(MyBase.DataGridView.SelectedRows(i).Cells(0).Value))
                            Me.countItemFat += 1
                        End If
                    Next
                Else
                    MessageBox.Show("Nenhuma linha selecionada.", Core.STR_NOME_SISTEMA)
                End If
        End Select
    End Sub

    Private Sub IndexGrid()
        Dim index As Integer = 1
        For Each row As DataGridViewRow In MyBase.DataGridView.Rows
            row.Cells(1).Value = index
            index += 1
        Next
    End Sub
End Class
