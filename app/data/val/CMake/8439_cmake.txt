# Copyright (c) 2011 Serge Ziryukin
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.

set(BACKENDS)
set(FSEVENT_BACKEND)

macro(try_this NAME VARIABLE SOURCE)
  set(SRC ${CMAKE_CURRENT_SOURCE_DIR}/cmake/src)
  set(BACKENDS ${BACKENDS} ${NAME})

  try_run(${VARIABLE}_RUN ${VARIABLE}_COMPILE
    ${CMAKE_BINARY_DIR}
    ${SRC}/${SOURCE}
    OUTPUT_VARIABLE ${NAME}_OUTPUT)

  if(${VARIABLE}_COMPILE AND ${VARIABLE}_RUN EQUAL 0)
    set(${VARIABLE} YES)
    set(FSEVENT_BACKEND ${NAME})
  else()
    set(${VARIABLE} NO)
  endif()

  message(STATUS "${NAME}: ${${VARIABLE}}")
endmacro()

# Windows
try_this("ReadDirectoryChanges"
  HAVE_READ_DIRECTORY_CHANGES
  have_read_directory_changes.c)

# kqueue
try_this("kqueue"
  HAVE_KQUEUE
  have_kqueue.c)

# Linux
try_this("inotify"
  HAVE_INOTIFY
  have_inotify.c)

if(FSEVENT_BACKEND)
  message(STATUS "using '${FSEVENT_BACKEND}' backend")
else()
  message(SEND_ERROR "failed to find a proper backend")

  foreach(OUTPUT_VAR ${BACKENDS})
    message("=======================================")
    message("${OUTPUT_VAR}:\n${${OUTPUT_VAR}_OUTPUT}")
  endforeach()
endif()
