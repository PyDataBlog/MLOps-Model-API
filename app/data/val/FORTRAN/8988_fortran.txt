      SUBROUTINE DESCRI
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO EXTRACT DESCRIPTOR ITEMS FROM AN IMAGE AND ASSIGN THE VALUES
*	TO STARLINK PARAMETERS
*
*METHOD
*	OBTAIN IMAGE FRAME, EXTRACT DESCRIPTOR ITEMS. IF REQUIRED, PRINT
*	VALUES. ASSIGN VALUES TO STARLINK PARAMETERS
*
*ARGUMENTS
*	NONE
*
*STARLINK PARAMETERS
*	ILEVEL
*		INTERACTION LEVEL: CONTROLS PRINTING OF RESULTS
*	IMAGE
*		THE IMAGE
*	TITLE
*		OUTPUT PARAMETER: IMAGE TITLE
*	NAXIS1,NAXIS2
*		OUTPUT PARAMETERS: IMAGE DIMENSIONS
*	INVAL
*		OUTPUT PARAMETER: INVALID PIXEL FLAG FOR IMAGE
*	BSCALE,BZERO
*		OUTPUT PARAMETERS: IMAGE SCALE AND ZERO LEVEL
*
*CALLS
*	THIS PACKAGE:
*		GETPAR,GT2DIR,GTDSCR,LBGONE
*	STARLINK:
*		WRUSER,WRKEYC,WRKEYI,WRKEYR,FRDATA
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      INTEGER NPIX(1),NLINES(1),INVAL(1)
      CHARACTER TITLE(1)*30,CVAL*1,PRBUF*80
      REAL SCALE(1),ZERO(1)
C
C GET INTERACTION LEVEL
C
      ILEVEL=2
      CALL GETPAR('ILEVEL','INTEGER',1,1.0,3.0,.TRUE.,ILEVEL,RVAL,
     +IERR)
C
C OBTAIN INPUT IMAGE
C
      CALL GT2DIR('IMAGE',102,.FALSE.,NPIX(1),NLINES(1),IPIN,IERR)
      IF(IERR.EQ.0) THEN
C
C IMAGE OBTAINED SUCCESSFULLY...EXTRACT DESCRIPTOR ITEMS
C
	TITLE(1)=' '
        INVAL(1)=-100000
	SCALE(1)=1.0
	ZERO(1)=0.0
	CALL GTDSCR('IMAGE','TITLE','CHARACTER',IVAL,RVAL,TITLE(1),
     +	IERR)
	CALL GTDSCR('IMAGE','INVAL','INTEGER',INVAL(1),RVAL,CVAL,IERR)
 	CALL GTDSCR('IMAGE','BSCALE','REAL',IVAL,SCALE(1),CVAL,IERR)
	CALL GTDSCR('IMAGE','BZERO','REAL',IVAL,ZERO(1),CVAL,IERR)
C
C IF ILEVEL.GE.2 SHOW THE ITEMS TO THE USER
C
	IF(ILEVEL.GE.2) THEN
	  CALL WRUSER(' ',ISTAT)
	  WRITE(PRBUF,14)TITLE(1)
   14	  FORMAT('   TITLE=',A)
	  CALL WRUSER(PRBUF,ISTAT)
	  CALL WRUSER(' ',ISTAT)
	  WRITE(PRBUF,15)NPIX(1)
   15	  FORMAT('   NUMBER OF PIXELS PER LINE=',I8)
          CALL LBGONE(PRBUF(31:))
	  CALL WRUSER(PRBUF,ISTAT)
	  CALL WRUSER(' ',ISTAT)
	  WRITE(PRBUF,16)NLINES(1)
   16	  FORMAT('   NUMBER OF LINES=',I8)
	  CALL LBGONE(PRBUF(21:))
	  CALL WRUSER(PRBUF,ISTAT)
	  CALL WRUSER(' ',ISTAT)
	  WRITE(PRBUF,17)INVAL(1)
   17	  FORMAT('   FLAG FOR INVALID PIXELS=',I8)
	  CALL LBGONE(PRBUF(29:))
	  CALL WRUSER(PRBUF,ISTAT)
	  CALL WRUSER(' ',ISTAT)
	  WRITE(PRBUF,18)SCALE(1)
   18	  FORMAT('   IMAGE SCALE FACTOR=',SS,G13.6)
	  CALL WRUSER(PRBUF,ISTAT)
	  CALL WRUSER(' ',ISTAT)
	  WRITE(PRBUF,19)ZERO(1)
   19	  FORMAT('   IMAGE ZERO LEVEL=',SS,G13.6)
	  CALL WRUSER(PRBUF,ISTAT)
	  CALL WRUSER(' ',ISTAT)
	ENDIF
C
C WRITE THE RESULTS TO THE ENVIRONMENT
C
	CALL WRKEYC('TITLE',TITLE,1,ISTAT)
	CALL WRKEYI('NAXIS1',NPIX,1,ISTAT)
	CALL WRKEYI('NAXIS2',NLINES,1,ISTAT)
	CALL WRKEYI('INVAL',INVAL,1,ISTAT)
	CALL WRKEYR('BSCALE',SCALE,1,ISTAT)
	CALL WRKEYR('BZERO',ZERO,1,ISTAT)
      ENDIF
C
C RELEASE IMAGE AND RETURN
C
      CALL FRDATA(' ',ISTAT)
      RETURN
      END
